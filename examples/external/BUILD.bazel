load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_run_binary")
load("@aspect_rules_js//npm:defs.bzl", "npm_link_package")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")
load("@bazel_skylib//rules:build_test.bzl", "build_test")
load(
    "@rules_prerender//:index.bzl",
    "link_prerender_component",
    "prerender_pages",
    "web_resources_devserver",
)
load("@rules_prerender_npm//:defs.bzl", "npm_link_all_packages")

copy_to_bin(
    name = "package",
    srcs = ["package.json"],
)

npm_link_all_packages(name = "node_modules")

npm_link_package(
    name = "node_modules/rules_prerender",
    src = "@rules_prerender//:rules_prerender_pkg",
    visibility = ["//visibility:public"],
)
npm_link_package(
    name = "node_modules/@rules_prerender/declarative_shadow_dom",
    src = "@rules_prerender//packages/declarative_shadow_dom:pkg",
    visibility = ["//visibility:public"],
)
link_prerender_component(
    name = "prerender_components/@rules_prerender/declarative_shadow_dom",
    package = ":node_modules/@rules_prerender/declarative_shadow_dom",
    visibility = ["//visibility:public"],
)
npm_link_package(
    name = "node_modules/@rules_prerender/preact",
    src = "@rules_prerender//packages/preact:pkg",
    visibility = ["//visibility:public"],
    deps = {
        "//:.aspect_rules_js/node_modules/rules_prerender": "rules_prerender",
    },
)

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
    visibility = ["//visibility:public"],
)

ts_config(
    name = "tsconfig_client",
    src = "tsconfig.client.json",
    visibility = ["//visibility:public"],
    deps = [":tsconfig"],
)

prerender_pages(
    name = "site",
    entry_point = "./site.js",
    prerender = ":prerender",
)

ts_project(
    name = "prerender",
    srcs = ["site.tsx"],
    declaration = True,
    source_map = True,
    tsconfig = "//:tsconfig",
    # Need `"type": "module"` to load `*.js` files output by `*.tsx` compilation.
    data = [":package"],
    transpiler = "tsc",
    deps = [
        "//component:component_prerender",
        "//:node_modules/@rules_prerender/preact",
        "//:node_modules/preact",
    ],
)

web_resources_devserver(
    name = "devserver",
    resources = ":site",
)

build_test(
    name = "test",
    targets = [
        ":devserver",
        ":site",
    ],
)
